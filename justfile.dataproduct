set shell := ["bash", "-uc"]
ref_name := shell("git rev-parse --abbrev-ref HEAD")
name := shell("grep -Po '(?<=name: ).*$' model/*.linkml.yml | tr -d '\n' | tr '-' '_'")
site_title := shell("grep -Po '(?<=title: ).*$' model/*.linkml.yml | tr -d '\n'")

_default:
    @just --list --unsorted --justfile {{justfile()}}

# Generate documentation
[group("generators")]
generate-documentation: (_clean "output")
    @echo "Generating documentation…"
    @echo
    mkdir -p "output"
    cp -r "documentation" "output/adoc"
    @echo
    @echo "Generating schema documentation…"
    @echo
    mkdir -p "output/adoc/modules/schema"
    python -m linkml_asciidoc_generator.main \
        "model/{{name}}.linkml.yml" \
        "output/adoc/modules/schema" \
        "--relations-diagrams"
    @echo "Adding schema documentation to nav…"
    yq -i '.nav += ["modules/schema/nav.adoc"]' output/adoc/antora.yml
    @echo
    @echo -e "Copying generated artifacts to schema documentation…"
    @for model in model/*; do \
        cp -r $model output/adoc/modules/schema/attachments/; \
        echo -e "To reference use:\n\txref:schema:attachment$"${model#model/}"[]"; \
    done
    @echo -e "Copy examples (JSON) to schema documentation…"
    for example in examples/*.yml; do \
        example_name=$(basename $example); \
        gen-linkml-profile  \
            convert \
            "$example" \
            --out "output/adoc/modules/schema/attachments/${example_name%.*}.json"; \
        echo -e "To reference use:\n\txref:schema:attachment\$${example_name%.*}.json[]"; \
    done
    @echo
    @echo "Generating site using Antora…"
    @if [ ${IN_DEVCONTAINER} == "true"  ]; then \
        jinja -D site_url http://localhost:8080 -D site_title {{site_title}} /antora/local-antora-playbook.yml.jinja > /tmp/local-antora-playbook.yml; \
        antora /tmp/local-antora-playbook.yml; \
    else \
        jinja -D site_url http://modellen.netbeheernederland.nl -D site_title {{site_title}} /antora/antora-playbook.yml.jinja > /tmp/antora-playbook.yml; \
        antora /tmp/antora-playbook.yml; \
    fi
    @echo
    @echo -e "Generated documentation files at: output/html"
    @echo

# Clean up the provided artifact directory
[group("project")]
_clean path:
    @echo "Cleaning up…"
    @echo
    @if [ -d {{path}} ]; then \
        rm -rf {{path}}; \
    fi
    @echo "… OK."
    @echo

# Generate JSON Schema
[group("generators")]
generate-json-schema: (_clean "model/{{name}}.json_schema.json")
    @echo "Generating JSON Schema…"
    @echo
    gen-json-schema \
        --not-closed \
        "model/{{name}}.linkml.yml" \
        > "model/{{name}}.json_schema.json"
    @echo "… OK."
    @echo
    @echo "Generated JSON Schema at: model/{{name}}.json_schema.json"
    @echo

# Validate example data
[group("project")]
validate-example-data: generate-json-schema
    @echo "Validating example data against JSON schema…"
    @echo
    for example in examples/*.yml; do \
        example_json=/tmp/{{name}}/examples/${example%.*}.json; \
        gen-linkml-profile  \
            convert \
            "$example" \
            --out $example_json; \
        check-jsonschema --schemafile "model/{{name}}.json_schema.json" $example_json; \
    done
    @echo "… OK."
    @echo

# Serve the generated website
[group("project")]
serve:
    http-serve output/html

# Release new major version
[group("version-control")]
release-major-version: && generate-documentation #validate-example-data
    @echo "Releasing new major version…"
    @echo
    just build
    git add && git commit
    git tag v1.0
    gh workflow run deploy.yml --ref {{ref_name}}
    @echo "… OK."
    @echo

# Release new minor version
[group("version-control")]
release-minor-version:
    @echo "Releasing new minor version…"
    @echo
    gh workflow run release_minor_version.yml --ref {{ref_name}}
    @echo "… OK."
    @echo
